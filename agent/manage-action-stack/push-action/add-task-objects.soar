sp {push-action*propose*add-task-objects
  (state <s> ^name push-action
             ^action-to-push <action>
             -^added-objects)
-->
  (<s> ^operator <o> + >)
  (<o> ^name add-task-objects)
}

sp {push-action*elaborate*add-task-objects*object
  (state <s> ^name push-action
             ^operator <o> +
             ^action-to-push.execution-operator.<arg-name> <arg>)
  (<o> ^name add-task-objects)
  (<arg> ^arg-type object
         ^id <obj>)
-->
  (<o> ^object <obj>)
}

sp {push-action*elaborate*add-task-objects*object*from*predicate
   (state <s> ^name push-action
              ^operator <o> +
              ^action-to-push.execution-operator.<arg-name> <arg>)
   (<o> ^name add-task-objects)
   (<arg> ^arg-type predicate
          ^2 <obj>)
-->
   (<o> ^object <obj>)
}

sp {push-action*elaborate*add-taks-objects*object*from*until-clause
   (state <s> ^name push-action
              ^operator <o> +
              ^action-to-push.execution-operator.until-clause <until>)
   (<o> ^name add-task-objects)
   (<until> ^predicate.{ <num> << 1 2 >> } <obj>)
-->
   (<o> ^object <obj>)
}

sp {push-action*apply*add-task-objects*no*objects
  (state <s> ^name push-action
             ^operator.name add-task-objects
             ^action-to-push <action>)
-->
  (<s> ^added-objects true)
}

sp {push-action*apply*add-task-objects*copy*object
  (state <s> ^name push-action
             ^operator <o>
             ^action-stack.task-objects <objs>)
  (<o> ^name add-task-objects
       ^object <obj>)
-->
  (<objs> ^object <obj>)
}

