##### operator create-action-operator
# creates the basic structure for the operator as action-operator

# propose create-action-operator
sp {generate-action-operator*propose*create-action-operator
   (state <s> ^name generate-action-operator
              ^action-id <id>
             -^action-operator.name)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-action-operator
        ^action-id <id>)
} 

# create the action-operator strucutre with action-handle
sp {generate-action-operator*apply*create-action-operator
   (state <s> ^name generate-action-operator
              ^operator <o>)
   (<o> ^name create-action-operator
        ^action-id <id>)
   (<id> ^handle <action-handle>)
-->
   (<s> ^action-operator <op>)
   (<op> ^action-handle <action-handle>)
}

sp {generate-action-operator*apply*create-action-operator*create*subaction-handle*none
   (state <s> ^name generate-action-operator
              ^operator.name create-action-operator
              ^top-state.action-stack.top none
              ^action-operator <a>)
-->
   (<a> ^subaction-handle none)
}

# Copy the name of the operator
sp {generate-action-operator*apply*create-action-operator*copy*operator*name
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-action-operator
        ^action-id.procedural.op_name <name>)
-->
   (<op> ^name <name>)
}

# Copy the perform flag
sp {generate-action-operator*apply*create-action-operator*copy*perform
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-action-operator
        ^action-id.perform <perf>)
-->
   (<op> ^perform <perf>)
}

sp {generate-action-operator*apply*create-action-operator*copy*perform*default
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-action-operator
        ^action-id <aid>)
   (<aid> -^perform <perf>)
-->
   (<op> ^perform multiple)
}

sp {generate-action-operator*apply*create-action-operator*mark*learn-proposal
   (state <s> ^name generate-action-operator
              ^operator <o>
              ^action-operator <op>)
   (<o> ^name create-action-operator
        ^action-id.learn-proposal <bool>)
-->
  (<op> ^learn-proposal <bool>)
}
