sp {block*unchecked-operator
	(state <s> ^operator (<o> ^action-handle <ah> -^approved) +)
-->
	(<s> ^operator <o> -)}

sp {governor*elaborate*action-stack
	(state <s> ^top-state.action-stack <stack>
			^superstate.name governor-selection
			-^name execute-action)
-->
	(<s> ^action-stack <stack>)}

sp {governor*child-action*thing
	(state <s> -^name execute-action
				^child-action <a>
				^superstate <ss>)
	(<ss> ^operator <o> +)
	(<o> ^evaluated success)
	(<a> ^execution-operator <o1>)
	(<s> ^operator { <o2> <> <o1> } +)
	-(<o2> ^name dummy)
-->
	(<s> ^operator <o2> -)
	(<o2> ^rejected-from governor)}

sp {ohmygod*reject
	(state <s> ^operator <o> +)
	(<o> ^rejected-from governor)
-->
	(<s> ^operator <o> -)}

sp {need*dummy
	(state <s> ^operator <o> +
				-^name evaluate-operator
				^top-state.labels <lables>)
	(<o> -^evaluated ^action-handle <ah> ^name <action-name>)
	(<labels> -^<action-name> ok)
-->
	(<o> ^needs-evaluation yes)
	(<s> ^need-dummy yes)}

#introduce a dummy operator if needed
sp {propose*dummy
	(state <s> ^need-dummy yes)
-->
	(<s> ^operator <o> +)
	(<o> ^name dummy
		^evaluated failure)}

##set the dummy operator to be better and worse than all operators
##approve the operator so the conflict will trigger an impasse - 
#if it is found to violate constraints, it will evaluate as a failure, which will reject it
sp {force*conflict
	(state <s> ^operator (<dum> ^name dummy) +
				^operator (<o> ^needs-evaluation yes) +)
-->
	(<s> ^operator <dum> > <o>
		^operator <dum> < <o>)
	(<o> ^approved evaluating)}

sp {approve*non-restricted*actions
	:o-support
	(state <s> ^operator <o> + ^top-state.labels <labels>)
	(<labels> ^name action-labels) 
	(<o> ^name execute-action -^changed yes -^name dummy)
	-{(<o> ^action.execution-operator.name <action-name>)
		(<labels> -^<action-name> ok)}
-->
	(<o> ^evaluated success)}

#If all operators have been evaluated, approve the operators that were a success
sp {approve*if*success
	(state <s> ^operator (<o> ^evaluated success) +)
	-(<s> ^operator (-^evaluated ^name execute-action) +)
-->
	(<o> ^approved yes)}

sp {wait*to*approve*auto-successes
	(state <s> ^operator <o> +
				^operator { <> <o> <o2> } +)
	(<o2> -^evaluated ^actions <a>)
	(<o> ^evaluated success)
-->
	(<s> ^operator <o> -)}

sp {reject*dummy
	(state <s> ^operator (<dum> ^name dummy) +)
	-(<s> ^operator (-^evaluated) +)
-->
	(<s> ^operator <dum> -)}
