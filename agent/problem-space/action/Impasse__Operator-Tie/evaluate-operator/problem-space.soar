########## evaluate-operator state #########

sp {problem-space*action*evaluate-operator*elaborate*problem-space*execution-type*internal
   (state <s> ^name evaluate-operator
              ^problem-space <ps>)
   (<ps> ^name action)
-->
   (<ps> ^execution-type internal)
}

sp {problem-space*action*evaluate-operator*elaborate*problem-space*default-copy*info
   (state <s> ^name evaluate-operator
              ^problem-space <ps>)
   (<ps> ^name action
        -^action-space waypoint-search)
-->
   (<ps> ^default-state-copy no
         ^default-operator-copy custom)
}

sp {problem-space*action*evaluate-operator*elaborate*problem-space*world*copy-type*deep
   (state <s> ^name evaluate-operator
              ^problem-space <ps>)
   (<ps> ^name action
        -^action-space waypoint-search
         ^world <world>)
-->
   (<world> ^copy-type deep)
}

sp {problem-space*action*evaluate-operator*elaborate*problem-space*subactions*copy-type*deep
   (state <s> ^name evaluate-operator
              ^problem-space <ps>)
   (<ps> ^name action
        -^action-space waypoint-search
         ^subactions <subs>)
-->
   (<subs> ^copy-type deep)
}

sp {problem-space*action*evaluate-operator*elaborate*propose-action-operators*true
   (state <s> ^name evaluate-operator
              ^problem-space <ps>)
   (<ps> ^name action
         ^learning.type exploration)
-->
   (<ps> ^propose-action-operators true)
}


## If the desired goal is satisfied, report success
sp {problem-space*action*evaluate-operator*elaborate*successs*desired*satisfied
   (state <s> ^name evaluate-operator
              ^problem-space.name action
              ^desired <d>)
   (<d> ^satisfied true)
-->
   (<s> ^success <d>)
}

sp {problem-space*action*evaluate-operator*elaborate*success*if*desired*not*duplicate
   (state <s> ^name evaluate-operator
              ^problem-space.name action
              ^desired <des>
              ^success <des>
              ^superstate <sel>)
   (<des> -^duplicate-of <any>)
   (<sel> ^evaluation <eval>
          ^operator <o>)
   (<o> ^evaluation <eval>)
-->
   (<eval> ^symbolic-value success)
}
