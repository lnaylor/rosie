sp {learn-subaction-policy*action-operator*propose*report-success
   (state <s> ^problem-space.action-space retrospective
              ^superstate.name learn-subaction-policy
              ^desired <des>)
   (<des> ^satisfied true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name report-success)
}

sp {learn-subaction-policy*action-operator*apply*report-success
   (state <s> ^problem-space.action-space retrospective
              ^superstate.name learn-subaction-policy
              ^operator.name report-success
              ^superstate <ss>)
-->
   (<ss> ^retrospective-learning-result success)
}
             
sp {learn-subaction-policy*action-operator*prefer*report-success*over*all*others
   (state <s> ^problem-space.action-space retrospective
              ^superstate.name learn-subaction-policy
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name report-success)
   (<o2> ^name <> report-success)
-->
   (<s> ^operator <o1> > <o2>)
}
