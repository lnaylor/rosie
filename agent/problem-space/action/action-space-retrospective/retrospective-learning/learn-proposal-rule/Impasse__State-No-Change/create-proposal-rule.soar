# If the operator was successful, propose it on the superstate
# (Making sure to go backwards in the deep-copy-objects map, otherwise chunking EXPLODES)

# ENUMERATE: 0-2 args
sp {learn-proposal-rule-substate*learn*action*proposal*rule*0*args
   (state <s> ^name learn-proposal-rule-substate
              ^operator-successful true
              ^operator <o>
              ^arg-info.num-args 0
              ^superstate <ss>)
   (<o> ^name <name>
        ^action-handle <h>
        ^perform <perf>
       -^<arg-name>.arg-type <t>)
-->
   (<ss> ^operator <op-copy> +)
   (<op-copy> ^name <name>
              ^action-handle <h>
              ^subaction-handle none
              ^perform <perf>)
}

sp {learn-proposal-rule-substate*learn*action*proposal*rule*1*args
   (state <s> ^name learn-proposal-rule-substate
              ^operator-successful true
              ^operator <o>
              ^arg-info <info>
              ^superstate <ss>)
   (<info> ^num-args 1
           ^1 <arg1-name>)
   (<o> ^name <name>
        ^action-handle <h>
        ^perform <perf>
        ^<arg1-name>.ss-version <arg1>)
-->
   (<ss> ^operator <op-copy> +)
   (<op-copy> ^name <name>
              ^action-handle <h>
              ^subaction-handle none
              ^perform <perf>
              ^<arg1-name> <arg1>)
}

sp {learn-proposal-rule-substate*learn*action*proposal*rule*2*args
   (state <s> ^name learn-proposal-rule-substate
              ^operator-successful true
              ^operator <o>
              ^arg-info <info>
              ^superstate <ss>)
   (<info> ^num-args 2
           ^1 <arg1-name>
           ^2 <arg2-name>)
   (<o> ^name <name>
        ^action-handle <h>
        ^perform <perf>
        ^<arg1-name>.ss-version <arg1>
        ^<arg2-name>.ss-version <arg2>)
-->
   (<ss> ^operator <op-copy> +)
   (<op-copy> ^name <name>
              ^action-handle <h>
              ^subaction-handle none
              ^perform <perf>
              ^<arg1-name> <arg1>
              ^<arg2-name> <arg2>)
}

