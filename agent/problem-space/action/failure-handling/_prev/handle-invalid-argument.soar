
# invalid-argument: The LTI found for the direction-handle is not a relative-direction
sp {op_turn1*elaborate*failure*invalid-argument*relative-direction
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^direction-handle <handle>
              ^direction-id.property.handle <> relative-direction1)
-->
   (<s> ^failure <f>)
   (<f> ^type invalid-argument
        ^argument arg1
        ^expected-type relative-direction1)
}

sp {action-space*execute*handle-action-failure*propose*handle-invalid-argument
   (state <s> ^name handle-action-failure
              ^failure-type invalid-argument
             -^terminate-action true)
-->
   (<s> ^operator <o> +)
   (<o> ^name handle-invalid-argument)
}

sp {action-space*execute*handle-action-failure*apply*handle-invalid-argument*terminate-action
   (state <s> ^name handle-action-failure
              ^operator.name handle-invalid-argument)
-->
   (<s> ^terminate-action true)
}
       
sp {action-space*execute*handle-action-failure*apply*handle-invalid-argument*send-message
   (state <s> ^name handle-action-failure
              ^operator.name handle-invalid-argument
              ^failure <f>)
   (<f> ^argument <arg>
        ^expected-type <type>)
-->
   (<s> ^terminate-action true)
}       

sp {action-space*execute*handle-action-failure*invalid-argument*elaborate*terminate-action
   (state <s> ^name handle-action-failure
              ^failure-type invalid-argument
              ^failure
              ^current-action.command-failed once)
-->
   (<s> ^terminate-action true)
}
