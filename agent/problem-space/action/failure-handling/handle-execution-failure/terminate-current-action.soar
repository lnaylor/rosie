sp {handle-execution-failure*propose*terminate-current-action
   (state <s> ^name handle-execution-failure
              ^action-segment <a>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name terminate-current-action)
}

sp {handle-execution-failure*apply*terminate-current-action*add*failure*status
   (state <s> ^name handle-execution-failure
              ^operator.name terminate-current-action
              ^action-segment <a>)
-->
   (<a> ^unrecoverable-failure execution-failure)
}

sp {handle-execution-failure*apply*terminate-current-action*send*message*execution-failure
   (state <s> ^name handle-execution-failure
              ^operator.name terminate-current-action
             -^silent true
              ^action-segment.execution-operator.action-handle <action-h>
             -^failure-subtype unsatisfied-goal
              ^failed-command.name <cmd-name>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type execution-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^action-handle <action-h>
        ^command-name <cmd-name>)
}

sp {handle-execution-failure*apply*terminate-current-action*send*message*unsatisfied-goal
   (state <s> ^name handle-execution-failure
              ^operator.name terminate-current-action
             -^silent true
              ^action-segment.execution-operator.action-handle <action-h>
              ^failure-subtype unsatisfied-goal
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type unsatisfied-goal
          ^complete-message true
          ^fields <f>)
   (<f> ^action-handle <action-h>)
}
