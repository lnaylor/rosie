sp {handle-missing-object-failure*propose*terminate-current-action
   (state <s> ^name handle-missing-object-failure
              ^action-segment <a>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name terminate-current-action)
}

sp {handle-missing-object-failure*apply*terminate-current-action*add*failure*status
   (state <s> ^name handle-missing-object-failure
              ^operator.name terminate-current-action
              ^action-segment <a>)
-->
   (<a> ^unrecoverable-failure missing-object-failure)
}

sp {handle-missing-object-failure*apply*terminate-current-action*send*message*missing-object-failure
   (state <s> ^name handle-missing-object-failure
              ^operator.name terminate-current-action
             -^silent true
              ^action-segment.execution-operator.action-handle <action-h>
              ^missing-object <obj>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type missing-object-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^action-handle <action-h>
        ^missing-object <obj>)
}
