sp {action-utils*copy-action*propose*complete-copy-action
   (state <s> ^copy-action-workspace <ws>)
-->
   (<s> ^operator <o> + < =)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
}

sp {action-utils*copy-action*elaborate*complete-copy-action*perform*copy
   (state <s> ^operator <o> +)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^source-action.perform <perf>)
-->
   (<ws> ^perform <perf>)
}

sp {action-utils*copy-action*elaborate*complete-copy-action*perform*default
   (state <s> ^operator <o> +)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^source-action <src>)
   (<src> -^perform)
-->
   (<ws> ^perform multiple)
}

sp {action-utils*copy-action*prefer*higher*workspace
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << complete-copy-action copy-action-argument >>
         ^workspace <ws>)
   (<o2> ^name << complete-copy-action copy-action-argument >>
         ^workspace < <ws>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {action-utils*copy-action*apply*complete-copy-action*reject
   (state <s> ^operator <o> +)
   (<o> ^name << complete-copy-action copy-action-argument >>
        ^workspace <ws>)
   (<ws> ^copy-name <name>
         ^destination.<name> <copy>)
-->
   (<s> ^operator <o> -)
}

sp {action-utils*copy-action*apply*complete-copy-action*create*copied-action*no*subaction-handle
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^source-action <src>
         ^perform <perf>)
   (<src> ^name <name>
          ^action-handle <h>
          -^subaction-handle <sub-h>)
-->
   (<ws> ^copied-action <copy>)
   (<copy> ^name <name>
           ^action-handle <h>
           ^perform <perf>)
}

sp {action-utils*copy-action*apply*complete-copy-action*create*copied-action*subaction-handle
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^source-action <src>
         ^perform <perf>)
   (<src> ^name <name>
          ^action-handle <h>
          ^subaction-handle <sub-h>)
-->
   (<ws> ^copied-action <copy>)
   (<copy> ^name <name>
           ^action-handle <h>
           ^perform <perf>
           ^subaction-handle <sub-h>)
}

# Also, arg-status
sp {action-utils*copy-action*apply*complete-copy-action*copy*arg-status
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^source-arg.arg-status <status>
          ^arg-copy <arg-copy>)
-->
   (<arg-copy> ^arg-status <status>)
}
sp {action-utils*copy-action*apply*complete-copy-action*copy*arg-status*unknown
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^copy-argument <arg>)
   (<arg> ^source-arg <src>
          ^arg-copy <arg-copy>)
   (<src> -^arg-status)
-->
   (<arg-copy> ^arg-status unknown)
}


# Copy the arguments onto the copied-action and copy the copied-action to the superstate
# (Will create the chunked application rule)
# ENUMERATE: 0-5 arguments

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*0*args
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^num-args 0
         ^not-copied true
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
-->
   (<dest> ^<name> <copy>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*1*args
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^num-args 1
         ^not-copied true
         ^copy-argument   <arg1>
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*2*args
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^num-args 2
         ^not-copied true
         ^copy-argument   <arg1>
         ^copy-argument { <arg2> > <arg1>}
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
   (<arg2> ^arg-name <arg2-name> ^arg-copy <arg-copy2>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>
		   ^<arg2-name> <arg-copy2>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*3*args
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^num-args 3
         ^not-copied true
         ^copy-argument   <arg1>
         ^copy-argument { <arg2> > <arg1>}
         ^copy-argument { <arg3> > <arg2>}
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
   (<arg2> ^arg-name <arg2-name> ^arg-copy <arg-copy2>)
   (<arg3> ^arg-name <arg3-name> ^arg-copy <arg-copy3>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>
		   ^<arg2-name> <arg-copy2>
		   ^<arg3-name> <arg-copy3>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*4*args
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^num-args 4
         ^not-copied true
         ^copy-argument   <arg1>
         ^copy-argument { <arg2> > <arg1>}
         ^copy-argument { <arg3> > <arg2>}
         ^copy-argument { <arg4> > <arg3>}
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
   (<arg2> ^arg-name <arg2-name> ^arg-copy <arg-copy2>)
   (<arg3> ^arg-name <arg3-name> ^arg-copy <arg-copy3>)
   (<arg4> ^arg-name <arg4-name> ^arg-copy <arg-copy4>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>
	  	   ^<arg2-name> <arg-copy2>
	   	   ^<arg3-name> <arg-copy3>
		   ^<arg4-name> <arg-copy4>)
}

sp {copy-action*complete-copy-action*learn-copy-action-elaboration-rule*5*args
   (state <s> ^operator <o>)
   (<o> ^name complete-copy-action
        ^workspace <ws>)
   (<ws> ^num-args 5
         ^not-copied true
         ^copy-argument   <arg1>
         ^copy-argument { <arg2> > <arg1>}
         ^copy-argument { <arg3> > <arg2>}
         ^copy-argument { <arg4> > <arg3>}
         ^copy-argument { <arg5> > <arg4>}
         ^copied-action <copy>
         ^destination <dest>
         ^copy-name <name>)
   (<arg1> ^arg-name <arg1-name> ^arg-copy <arg-copy1>)
   (<arg2> ^arg-name <arg2-name> ^arg-copy <arg-copy2>)
   (<arg3> ^arg-name <arg3-name> ^arg-copy <arg-copy3>)
   (<arg4> ^arg-name <arg4-name> ^arg-copy <arg-copy4>)
   (<arg5> ^arg-name <arg5-name> ^arg-copy <arg-copy5>)
-->
   (<dest> ^<name> <copy>)
   (<copy> ^<arg1-name> <arg-copy1>
			  ^<arg2-name> <arg-copy2>
			  ^<arg3-name> <arg-copy3>
			  ^<arg4-name> <arg-copy4>
			  ^<arg5-name> <arg-copy5>)
}
