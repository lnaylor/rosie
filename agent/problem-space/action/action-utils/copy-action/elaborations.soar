sp {anystate*copy-action*elaborate*performed*info
   (state <s> ^copy-action <ws>)
   (<ws> ^source-action.performed <sub>
         ^destination <dest>
         ^copy-name <name>)
   (<dest> ^<name> <copy>)
-->
   (<copy> ^performed <sub>)
}

sp {state*no-change*copy-action*elaborate*copy-action-workspace
   (state <s> ^superstate.copy-action <ssca>
              ^impasse no-change
              ^attribute state)
-->
   (<s> ^copy-action-workspace <ws>)
   (<ws> ^ss-copy-action <ssca>)
}

sp {action-utils*copy-action*elaborate*name*learn-copy-action-elaboration-rule
   (state <s> ^copy-action-workspace <ws>)
-->
   (<s> ^name learn-copy-action-elaboration-rule)
}

sp {action-utils*copy-action*elaborate*learning-action-utils-rule
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action-workspace <ws>)
   (<ws> ^copy-name <name>
         ^destination <dest>)
   (<dest> -^<name> <copy>)
-->
   (<s> ^learning-action-utils-rule true)
}

sp {copy-action*op-no-change*elaborate*ss-copy-action*superoperator
   (state <s> ^name copy-action
              ^superstate.operator <o>)
-->
   (<s> ^copy-action-workspace <ws>)
   (<ws> ^ss-copy-action <o>)
}

sp {action-utils*copy-action*elaborate*info
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^ss-copy-action <ssca>)
   (<ssca> ^source-action <src-action>
           ^destination <dest>
           ^copy-name <name>)
-->
   (<ws> ^source-action <src-action>
         ^destination <dest>
         ^copy-name <name>)
}

sp {learn-copy-action-elaborate-rule*elaborate*copy-action*not-copied*true
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^copy-name <name>
        -^destination.<name>)
-->
   (<ws> ^not-copied true)
}

sp {action-utils*copy-action*elaborate*object-map*superoperator
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^ss-copy-action <ssca>)
   (<ssca> ^object-map <map>)
-->
   (<ws> ^object-map <map>)
}

sp {action-utils*copy-action*elaborate*object-map*none
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^ss-copy-action <ssca>)
   (<ssca> -^object-map <map>)
-->
   (<ws> ^no-object-map true)
}

# elaborate argument information 
## ENUMERATE: 0-5 arguments
sp {action-utils*copy-action*elaborate*no-args
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^source-action <a>)
   (<a> -^{ << arg1 arg2 arg3 arg4 arg5 until-clause >> } <arg>)
-->
   (<ws> ^num-args 0)
}

sp {action-utils*copy-action*elaborate*arguments*1*arg
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^source-action <a>)
   (<a> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
       -^{ <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>)
   (<a1> ^arg-type <t1>)
--> 
   (<ws> ^num-args 1
        ^copy-argument <ca1>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
}

sp {action-utils*copy-action*elaborate*arguments*2*args
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^source-action <a>)
   (<a> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
       -^{ <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
-->
   (<ws> ^num-args 2
        ^copy-argument <ca1> <ca2>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
}

sp {action-utils*copy-action*elaborate*arguments*3*args
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^source-action <a>)
   (<a> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
       -^{ <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
-->
   (<ws> ^num-args 3
        ^copy-argument <ca1> <ca2> <ca3>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
}

sp {action-utils*copy-action*elaborate*arguments*4*args
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^source-action <a>)
   (<a> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2> < <n4> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
        ^{ <n4> > <n3>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
       -^{ <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
-->
   (<ws> ^num-args 4
        ^copy-argument <ca1> <ca2> <ca3> <ca4>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
   (<ca4> ^arg-type <t4>
          ^arg-name <n4>
          ^source-arg <a4>)
}


sp {action-utils*copy-action*elaborate*arguments*5*args
   (state <s> ^copy-action-workspace <ws>)
   (<ws> ^source-action <a>)
   (<a> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2> < <n4> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
        ^{ <n4> > <n3> < <n5> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
        ^{ <n5> > <n4>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>
       -^{ <n6> <> <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a6>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
   (<a5> ^arg-type <t5>)
-->
   (<ws> ^num-args 5
        ^copy-argument <ca1> <ca2> <ca3> <ca4> <ca5>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
   (<ca4> ^arg-type <t4>
          ^arg-name <n4>
          ^source-arg <a4>)
   (<ca5> ^arg-type <t5>
          ^arg-name <n5>
          ^source-arg <a5>)
}
