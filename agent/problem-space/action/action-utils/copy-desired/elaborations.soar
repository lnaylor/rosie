sp {state*no-change*copy-desired*elaborate*copy-desired-workspace
   (state <s> ^superstate.copy-desired <des>
              ^impasse no-change
              ^attribute state)
-->
   (<s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <des>)
}

sp {action-utils*copy-desired*elaborate*name*learn-copy-desired-elaboration-rule
   (state <s> ^copy-desired-workspace <ws>)
-->
   (<s> ^name learn-copy-desired-elaboration-rule)
}

sp {learn-copy-desired-elaborate-rule*elaborate*copy-desired*was-copied*true
   (state <s> ^copy-desired-workspace <ws>
              ^superstate.desired.duplicate-of <des>)
   (<ws> ^source-desired <des>)
-->
   (<ws> ^was-copied true)
}

sp {action-utils*copy-desired*elaborate*object-map*superoperator
   (state <s> ^copy-desired-workspace <ws>
              ^superstate.deep-copy-world.copy-objects-map <map>)
-->
   (<ws> ^object-map <map>)
}

sp {action-utils*copy-desired*elaborate*learning-action-utils-rule
   (state <s> ^name learn-copy-desired-elaboration-rule
              ^copy-desired-workspace <ws>)
   (<ws> -^was-copied true)
-->
   (<s> ^learning-action-utils-rule true)
}

# elaborate predicate information 
## ENUMERATE: 0-5 predicates
sp {action-utils*copy-desired*elaborate*predicates*0*preds
   (state <s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <des>)
   (<des> -^predicate <p1>)
-->
   (<ws> ^num-preds 0)
}

sp {action-utils*copy-desired*elaborate*predicates*1*preds
   (state <s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <des>)
   (<des> ^predicate   <p1>
         -^predicate { <p2> <> <p1> })
--> 
   (<ws> ^num-preds 1
         ^copy-predicate <cp1>)
   (<cp1> ^num 1 ^source-pred <p1>)
}

sp {action-utils*copy-desired*elaborate*predicates*2*preds
   (state <s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <des>)
   (<des> ^predicate { <p1>          < <p2> }
          ^predicate { <p2>  > <p1>         }
         -^predicate { <p3> <> <p2> <> <p1> })
--> 
   (<ws> ^num-preds 2
         ^copy-predicate <cp1> <cp2>)
   (<cp1> ^num 1 ^source-pred <p1>)
   (<cp2> ^num 2 ^source-pred <p2>)
}

sp {action-utils*copy-desired*elaborate*predicates*3*preds
   (state <s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <des>)
   (<des> ^predicate { <p1>          < <p2> }
          ^predicate { <p2>  > <p1>  < <p3> }
          ^predicate { <p3>  > <p2>         }
         -^predicate { <p4> <> <p3> <> <p2> <> <p1> })
--> 
   (<ws> ^num-preds 3
         ^copy-predicate <cp1> <cp2> <cp3>)
   (<cp1> ^num 1 ^source-pred <p1>)
   (<cp2> ^num 2 ^source-pred <p2>)
   (<cp3> ^num 3 ^source-pred <p3>)
}

sp {action-utils*copy-desired*elaborate*predicates*4*preds
   (state <s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <des>)
   (<des> ^predicate { <p1>          < <p2> }
          ^predicate { <p2>  > <p1>  < <p3> }
          ^predicate { <p3>  > <p2>  < <p4> }
          ^predicate { <p4>  > <p3>         }
         -^predicate { <p5> <> <p4> <> <p3> <> <p2> <> <p1> })
--> 
   (<ws> ^num-preds 4
         ^copy-predicate <cp1> <cp2> <cp3> <cp4>)
   (<cp1> ^num 1 ^source-pred <p1>)
   (<cp2> ^num 2 ^source-pred <p2>)
   (<cp3> ^num 3 ^source-pred <p3>)
   (<cp4> ^num 4 ^source-pred <p4>)
}


sp {action-utils*copy-desired*elaborate*predicates*5*preds
   (state <s> ^copy-desired-workspace <ws>)
   (<ws> ^source-desired <des>)
   (<des> ^predicate { <p1>          < <p2> }
          ^predicate { <p2>  > <p1>  < <p3> }
          ^predicate { <p3>  > <p2>  < <p4> }
          ^predicate { <p4>  > <p3>  < <p5> }
          ^predicate { <p5>  > <p4>         }
         -^predicate { <p6> <> <p5> <> <p4> <> <p3> <> <p2> <> <p1> })
--> 
   (<ws> ^num-preds 5
         ^copy-predicate <cp1> <cp2> <cp3> <cp4> <cp5>)
   (<cp1> ^num 1 ^source-pred <p1>)
   (<cp2> ^num 2 ^source-pred <p2>)
   (<cp3> ^num 3 ^source-pred <p3>)
   (<cp4> ^num 4 ^source-pred <p4>)
   (<cp5> ^num 5 ^source-pred <p5>)
}
