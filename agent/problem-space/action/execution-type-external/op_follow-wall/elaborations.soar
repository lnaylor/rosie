sp {op_follow-wall1*elaborate*attend-to-all-perception*yes
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_follow-wall1*elaborate*until-clause*from*superoperator
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^action-operator.until-clause <term>)
-->
   (<s> ^until-clause <term>)
}

sp {op_follow-wall1*elaborate*executing-command*true
   (state <s> ^name op_follow-wall1
              ^current-action.{ << stop-command follow-wall-command >> }.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_follow-wall1*elaborate*allow-wait*true
   (state <s> ^name op_follow-wall1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}

#### GOAL SATISFACTION ####
sp {op_follow-wall1*elaborate*desired*satisfied*no*until-clause
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^current-action.follow-wall-command.status success
             -^until-clause)
-->
   (<s> ^desired.satisfied true)
}

sp {op_follow-wall1*elaborate*desired*satisfied*until-clause
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^current-action.stop-command.status success
              ^until-clause.satisfied true)
-->
   (<s> ^desired.satisfied true)
}

### direction-handle
# Which wall to follow (left or right)

sp {op_follow-wall1*elaborate*direction-handle*from*action-operator
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^action-operator.arg1.id.predicates.relative-direction1 <dir-handle>)
-->
   (<s> ^direction-handle <dir-handle>)
}
                       
# default is left
sp {op_follow-wall1*elaborate*direction-handle*default
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              -^action-operator.arg1.id.predicates.relative-direction1 <dir-handle>)
-->
   (<s> ^direction-handle left1)
}

### direction-id
# The LTI with information about the wall direction
sp {op_follow-wall1*elaborate*smem-query-cue*direction
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^direction-handle <dir-handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2)
	(<cue> ^handle <dir-handle>)
}

sp {op_follow-wall1*elaborate*direction-id*from*retrieval
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
			  ^direction-handle <dir-handle>
              ^smem-query.result <dir-id>)
   (<dir-id> ^handle <dir-handle>)
-->
   (<s> ^direction-id <dir-id>)
}

### wall-side
# Elaborated from the direction-id, either 1 or -1
sp {op_follow-wall*elaborate*wall-side*from*direction-id
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^direction-id.side <side>)
-->
   (<s> ^wall-side <side>)
}
