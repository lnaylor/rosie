# elaborate problem-space attend-to-all-perception
sp {op_orient1*elaborate*problem-space*attend-to-all-perception*yes
   (state <s> ^name op_orient1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_orient1*elaborate*executing-command*true
   (state <s> ^name op_orient1
              ^current-action.orient-command.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_orient1*elaborate*allow-wait*true
   (state <s> ^name op_orient1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}

#### GOAL SATISFACTION ####
sp {op_orient1*elaborate*desired*satisfied
   (state <s> ^name op_orient1
              ^problem-space.execution-type external
              ^current-action.orient-command.status success)
-->
   (<s> ^desired.satisfied true)
}

### direction-handle
# The cardinal direction the agent should turn towards (defaults to north)

sp {op_orient1*elaborate*direction-handle*from*action-operator
   (state <s> ^name op_orient1
              ^problem-space.execution-type external
              ^action-operator.arg1.handle <dir-handle>)
-->
   (<s> ^direction-handle <dir-handle>)
}

sp {op_orient1*elaborate*direction-handle*default
   (state <s> ^name op_orient1
              ^problem-space.execution-type external
              -^action-operator.arg1.handle <dir-handle>)
-->
   (<s> ^direction-handle north1)
}

### direction-id
# The smem LTI with info about the direction to orient towards

sp {op_orient1*elaborate*smem-query-cue*direction
   (state <s> ^name op_orient1
              ^problem-space.execution-type external
			     ^direction-handle <dir-handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2)
	(<cue> ^handle <dir-handle>)
}

sp {op_orient1*elaborate*direction-id*from*retrieval
   (state <s> ^name op_orient1
              ^problem-space.execution-type external
              ^direction-handle <dir-handle>
              ^smem-query.result <dir-id>)
   (<dir-id> ^handle <dir-handle>)
-->
   (<s> ^direction-id <dir-id>)
}

