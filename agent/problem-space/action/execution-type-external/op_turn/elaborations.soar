# allow the agent to wait while commands are executing
sp {op_turn1*elaborate*propose-wait*during*command
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^current-action.turn-command <cmd>)
   (<cmd> ^status << sent executing >>)
-->
   (<s> ^propose-wait true)
}


### direction-handle
# The direction the agent should turn (defaults to right)
                                      
sp {op_turn1*elaborate*direction-handle*from*action-operator
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^action-operator.arg1.handle <dir-handle>)
-->
   (<s> ^direction-handle <dir-handle>)
}

# default is right1
sp {op_turn1*elaborate*direction-handle*default
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
             -^action-operator.arg1.handle)
-->
   (<s> ^direction-handle right1)
}

### direction-id
# The smem info about the direction to turn
#   (lookup using direction-handle)

sp {op_turn1*elaborate*smem-query-cue*direction
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^direction-handle <dir-handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2)
   (<cue> ^handle <dir-handle>)
}

sp {op_turn1*elaborate*direction-id*from*retrieval
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^direction-handle <dir-handle>
              ^smem-query.result <dir-id>)
   (<dir-id> ^handle <dir-handle>)
-->
   (<s> ^direction-id <dir-id>)
}

###### SUCCESS ######

# elaborate goal (desired) at satisfied - turn command reports it was successful
sp {op_turn1*elaborate*desired*satisfied
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^current-action.turn-command.status success)
-->
   (<s> ^desired.satisfied true)
}

###### FAILURE ######

#### Different failure cases that can arise during turn

# retrieval-failure, direction: No LTI was found with the given direction-handle
sp {op_turn1*elaborate*failure*retrieval-failure*direction
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^smem-query.result failure
              ^direction-handle <handle>)
-->
   (<s> ^failure <f>)
   (<f> ^type retrieval-failure
        ^retrieval-type direction
        ^handle <handle>)
}

# invalid-argument: The LTI found for the direction-handle is not a relative-direction
sp {op_turn1*elaborate*failure*invalid-argument*relative-direction
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^direction-handle <handle>
              ^direction-id.property.handle <> relative-direction1)
-->
   (<s> ^failure <f>)
   (<f> ^type invalid-argument
        ^argument arg1
        ^expected-type |relative direction|)
}

# execution-failure: The turn command got a failure status
sp {op_turn1*elaborate*failure*execution-failure
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^current-action.turn-command.status { <status> << error failure interrupted >> })
-->
   (<s> ^failure <f>)
   (<f> ^type execution-failure
        ^command orient
        ^error-status <status>)
}
