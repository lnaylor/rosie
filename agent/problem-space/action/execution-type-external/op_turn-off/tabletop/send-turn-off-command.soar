###############################################################################
#
# Inside the execute-turn-off1 substate, this will put a turn-off command on the output link
#
###############################################################################

sp {op_turn-off1*propose*send-turn-off-command
   (state <s> ^name op_turn-off1
             -^current-action.turn-off-command
              ^target-object <obj>)
   (<obj> ^predicates.activation1 on2)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-turn-off-command
        ^object <obj>
        ^state-predicate activation1)
}

sp {op_turn-off1*apply*send-turn-off-command
   (state <s> ^name op_turn-off1
              ^operator <o>
              ^current-action <a>
              ^top-state.io.output-link <out>)
   (<o> ^name send-turn-off-command
        ^object.handle <obj-handle>
        ^state-predicate <state-pred>)
-->
   (<out> ^set-state <set>)
   (<set> ^object-handle <obj-handle>
          ^name <state-pred>
          ^value off2)
   (<a> ^turn-off-command <set>)
}
