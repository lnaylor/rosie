sp {op_pick-up1*fetch*elaborate*problem-space*attend-to-all-perception
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_pick-up1*fetch*elaborate*target-object*superoperator
   (state <s> ^name op_pick-up1
              ^action-operator.arg1.id <obj>)
-->
   (<s> ^target-object <obj>)
}

#### GOAL SATISFACTION ####
sp {op_pick-up1*fetch*elaborate*desired*satisfied
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^current-action.pick-up-command.status success)
-->
   (<s> ^desired.satisfied true)
}

sp {op_pick-up1*fetch*elaborate*executing-command*true
   (state <s> ^name op_pick-up1
              ^current-action.{ << pick-up-command reset-command >> }.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_pick-up1*fetch*elaborate*allow-wait*true
   (state <s> ^name op_pick-up1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}
