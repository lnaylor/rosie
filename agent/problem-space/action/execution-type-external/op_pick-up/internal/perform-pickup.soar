sp {op_pick-up1*internal*propose*perform-pickup
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^target-object.predicates.arm-status not-grabbed)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-pickup)
}

sp {op_pick-up1*internal*apply*perform-pickup*change*holding-object
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^operator.name perform-pickup
              ^top-state.perception-monitor.input-link.self.arm <arm>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^target-object <obj>)
   (<obj-info> ^wm-obj <obj>
               ^perception-id <pid>)
   (<arm> ^holding-object { <old> <> <pid> })
-->
   (<arm> ^holding-object <old> -
          ^holding-object <pid>)
}

sp {op_pick-up1*internal*prefer*attend-to-scene-change*over*perform-pick-up
   (state <s> ^name op_pick-up1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change)
   (<o2> ^name perform-pickup)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_pick-up1*internal*apply*complete-action*remove*in*predicate
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^target-object <obj>
              ^top-state.world.predicates.predicate <in-pred>
              ^top-state.world.robot.current-location <loc>)
   (<o> ^name complete-action
        ^status success)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
-->
   (<in-pred> ^instance <i> -)
}

sp {op_pick-up1*internal*apply*complete-action*remove*holding*predicate
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^target-object <obj>
              ^top-state.world.predicates.predicate <in-pred>)
   (<o> ^name complete-action
        ^status success)
   (<in-pred> ^handle holding1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <person>)
-->
   (<in-pred> ^instance <i> -)
}
