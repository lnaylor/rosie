sp {op_pick-up1*tabletop*elaborate*problem-space*attend-to-all-perception
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_pick-up1*tabletop*elaborate*target-object*superoperator
   (state <s> ^name op_pick-up1
              ^action-operator.arg1.id <obj>)
-->
   (<s> ^target-object <obj>)
}

#### GOAL SATISFACTION ####
sp {op_pick-up1*tabletop*elaborate*desired*satisfied
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^current-action.pick-up-command.status success)
-->
   (<s> ^desired.satisfied true)
}

sp {op_pick-up1*elaborate*executing-command*true
   (state <s> ^name op_pick-up1
              ^current-action.{ << pick-up-command reset-command >> }.status sent)
-->
   (<s> ^executing-command true)
}

sp {op_pick-up1*elaborate*allow-wait*true
   (state <s> ^name op_pick-up1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}
