sp {op_pick-up1*tabletop*apply*complete-action*add*grabbed
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^target-object.predicates <preds>)
   (<o> ^name complete-action
        ^status success)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -
            ^arm-status not-grabbed -
            ^arm-status grabbed)
}

sp {op_pick-up1*tabletop*apply*complete-action*failure*remove*moving
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^target-object.predicates <preds>)
   (<o> ^name complete-action
        ^status failure)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -)
}

sp {op_pick-up1*tabletop*apply*complete-action*move*belief*to*arm
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^target-object.svs-obj.id <id>
              ^perception-monitor.input-link.self.pose <pose>
              ^top-state.svs.command <cmd>)
   (<o> ^name complete-action
        ^status success)
   (<pose> ^x <x> ^y <y> ^z <z>)
-->
   (<cmd> ^set_transform <xform>)
   (<xform> ^id <id>
            ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}
