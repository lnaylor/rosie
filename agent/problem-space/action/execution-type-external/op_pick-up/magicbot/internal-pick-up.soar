sp {op_pick-up1*magicbot*propose*internal-pick-up
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^target-object <obj>
              ^top-state.world.robot.arm.state internal
              -^current-action.pick-up-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name internal-pick-up
        ^object <obj>)
}

#sp {op_pick-up1*magicbot*apply*internal-pick-up*send-confirmation
#   (state <s> ^name op_pick-up1
#              ^operator <o>
#              ^current-action.received-yes true
#              ^top-state.interaction.changes <c>)
#   (<o> ^name internal-pick-up)
#-->
#   (<c> ^outgoing-message <msg>)
#   (<msg> ^type confirm-pick-up
#          ^complete-message true)
#}

sp {op_pick-up1*magicbot*apply*internal-pick-up*send-message
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^current-action <a>
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-pick-up
        ^object <obj>)
   (<a> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type get-item-request
          ^item <obj>)
   (<a> ^sent-message true)
}

sp {op_pick-up*magicbot*apply*internal-pick-up*mark*success
  (state <s> ^name op_pick-up1
             ^operator.name internal-pick-up
             ^target-object.predicates <preds>
             ^current-action <a>)
  (<a> ^received-yes true)
-->
  (<a> ^pick-up-command.status success)
  (<preds> ^arm-status grabbed
           ^arm-status not-grabbed -)
}

sp {op_pick-up1*magicbot*apply*internal-pick-up*delete*belief*obj
   (state <s> ^name op_pick-up1
              ^operator.name internal-pick-up
              ^current-action.received-yes
              ^target-object.svs-obj.id <id>
              ^top-state.svs.command <cmd>)
-->
   (<cmd> ^delete_node.id <id>)
}


