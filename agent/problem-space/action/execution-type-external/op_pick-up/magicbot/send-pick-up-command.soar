###############################################################################
#
# Inside the execute-pick-up1 substate, this will put a grab command on the output link
# If the pick-up fails and needs to be re-issued, this will cause that to occur
#
###############################################################################

sp {op_pick-up1*magicbot*propose*send-pick-up-command
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
               ^target-object <obj>
               ^distance-to-object <= 1.6
              -^current-action.pick-up-command
               ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^perception-id <pid>
               ^input-link-obj.tag-id <tag>
               ^wm-obj <obj>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-pick-up-command
        ^perception-id <tag>)
}

sp {op_pick-up1*magicbot*apply*send-pick-up-command*send
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^io.output-link <ol>
              ^current-action <a>)
   (<o> ^name send-pick-up-command
        ^perception-id <perc-id>)
-->
   (<ol> ^do-control-law <cmd>)
   (<cmd> ^name pick-up
          ^parameters.object-id <perc-id>
          ^termination-condition <term>)
   (<term> ^name holding
           ^parameters.object-id <perc-id>)
   (<a> ^pick-up-command <cmd>)
}
