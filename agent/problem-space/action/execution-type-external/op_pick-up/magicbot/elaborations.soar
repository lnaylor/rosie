sp {op_pick-up1*magicbot*magicbot*elaborate*problem-space*attend-to-all-perception*yes
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_pick-up1*magicbot*elaborate*target-object*superoperator
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^action-operator.arg1.id <obj>)
-->
   (<s> ^target-object <obj>)
}

sp {op_pick-up1*magicbot*elaborate*object-info
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^target-object <obj>)
   (<obj-info> ^wm-obj <obj>)
-->
   (<s> ^object-info <obj-info>)
}

sp {op_pick-up1*elaborate*executing-command*true
   (state <s> ^name op_pick-up1
              ^current-action.pick-up-command.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_pick-up1*elaborate*allow-wait*true
   (state <s> ^name op_pick-up1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}

#### GOAL SATISFACTION ####
sp {op_pick-up1*magicbot*elaborate*desired*satisfied
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^current-action.pick-up-command.status success)
-->
   (<s> ^desired.satisfied true)
}
