sp {op_find1*reject*op_scan*if*scanned
   (state <s> ^name op_find1
              ^operator <o> +
              ^world.robot.current-location.handle <handle>
              ^current-action.scanned <handle>
              -^child-action.execution-operator <o>)
   (<o> ^name op_scan1)
-->
   (<s> ^operator <o> -)
}

sp {op_find1*reject*op_explore*if*explored
   (state <s> ^name op_find1
              ^operator <o> +
              ^current-action.explored true
              -^child-action.execution-operator <o>)
   (<o> ^name op_explore1)
-->
   (<s> ^operator <o> -)
}

#sp {op_find1*prefer*learn-task-subaction*over*others
#   (state <s> ^name op_find1
#              ^operator <o1> +
#              ^operator <o2> +)
#   (<o1> ^name learn-task-subaction)
#   (<o2> ^category << stm ltm interaction exploration >>)
#-->
#   (<s> ^operator <o1> > <o2>)
#}

#########################################################
# Elaborate strategy categories, from specific to general
#########################################################

# Strategies based on knowledge in short term memory
sp {op_find1*categorize*stm
   (state <s> ^name op_find1
              ^operator <o> +)
   (<o> ^name << op_go-to1 op_face1 >> )
-->
   (<o> ^category stm)
}

# Strategies based on knowledge in long term memory
sp {op_find1*categorize*ltm
   (state <s> ^name op_find1
              ^operator <o> +)
   (<o> ^name << op_recall1 op_think1 >> )
-->
   (<o> ^category ltm)
}

# Strategies based on exploring the local environment
sp {op_find1*categorize*local-exploration
   (state <s> ^name op_find1
              ^operator <o> +)
   (<o> ^name << op_scan1 >> )
-->
   (<o> ^category local-exploration)
}

# Strategies for asking for help
sp {op_find1*categorize*interaction
   (state <s> ^name op_find1
              ^operator <o> +)
   (<o> ^name << initiate-interaction >> )
-->
   (<o> ^category interaction)
}

# Strategies based on exploring the entire environment
sp {op_find1*categorize*global-exploration
   (state <s> ^name op_find1
              ^operator <o> +)
   (<o> ^name << op_explore1 >> )
-->
   (<o> ^category global-exploration)
}

sp {op_find1*equal*preference*same*category
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^category <cat>)
   (<o2> ^category <cat>)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {op_find1*prefer*stm*over*others
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^category stm)
   (<o2> ^category << ltm local-exploration interaction global-exploration >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_find1*prefer*ltm*over*others
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^category ltm)
   (<o2> ^category << local-exploration interaction global-exploration >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_find1*prefer*local-exploration*over*others
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^category local-exploration)
   (<o2> ^category << interaction global-exploration >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_find1*prefer*interaction*over*others
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^category interaction)
   (<o2> ^category << global-exploration >>)
-->
   (<s> ^operator <o1> > <o2>)
}

