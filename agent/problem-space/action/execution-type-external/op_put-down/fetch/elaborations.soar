sp {op_put-down1*fetch*elaborate*problem-space*attend-to-all-perception
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_put-down1*fetch*elaborate*held-object*superoperator
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^action-operator.arg1.id <obj>)
-->
   (<s> ^held-object <obj>)
}

sp {op_put-down1*fetch*elaborate*predicate-handle*superoperator
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^action-operator.arg2.handle <pred-handle>)
-->
	(<s> ^predicate-handle <pred-handle>)
}

sp {op_put-down1*fetch*elaborate*destination*superoperator
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^action-operator.arg2.{2} <obj>)
-->
   (<s> ^destination <obj>)
}

sp {op_put-down1*fetch*elaborate*projection-result*from*current-action
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^current-action.projection-result <res>)
-->
   (<s> ^projection-result <res>)
}

sp {op_put-down1*fetch*elaborate*executing-command*true
   (state <s> ^name op_put-down1
              ^current-action.put-down-command.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_put-down1*fetch*elaborate*allow-wait*true
   (state <s> ^name op_put-down1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}

sp {op_put-down1*elaborate*predicate-satisfied*true
   (state <s> ^name op_put-down1
              ^predicate-handle <ph>
              ^held-object <obj>
              ^destination <dest>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle <ph>
           ^instance <i>)
   (<i> ^1 <obj> ^2 <dest>)
-->
   (<s> ^predicate-satisfied true)
}

##### GOAL SATISFACTION #####
sp {op_put-down1*tabletop*elaborate*desired*satisfied
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^current-action.put-down-command.status success
              ^held-object.predicates.visible true
              ^predicate-satisfied true)
-->
   (<s> ^desired.satisfied true)
}
