# Don't attend to missing-object on the object being grabbed
sp {op_put-down1*tabletop*reject*attend-to-missing-object*held*object
   (state <s> ^name op_put-down1
              ^operator <o> +
              ^held-object <obj>)
   (<o> ^name attend-to-scene-change
        ^change.type missing-object
        ^change.object-info.wm-obj <obj>)
-->
   (<s> ^operator <o> -)
}

# Don't attend to new objects while the arm is moving
# (only evaluate those that were added during the put down as candidates for missing)
sp {op_put-down1*tabletop*reject*attend-to-new-object
   (state <s> ^name op_put-down1
              ^current-action.put-down-command <cmd>
              ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type new-object)
   (<cmd> ^status << sent executing >>)
-->
   (<s> ^operator <o> -)
}

# Don't attend to anything while the arm is moving (except new perception objects)
sp {op_put-down1*tabletop*reject*attend-to-scene-change*all*during*put-down
   (state <s> ^name op_put-down1
              ^current-action.put-down-command <cmd>
              ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type { <any> <> new-perception-object }
        ^change.info.category object)
   (<cmd> ^status << sent executing >>)
-->
   (<s> ^operator <o> -)
}

# Don't attend at all to the object being put down up while the arm is moving
sp {op_put-down1*tabletop*reject*attend-to-scene-change*grabbed*object
   (state <s> ^name op_put-down1
              ^operator <o> +
              ^target-object.handle <handle>
              ^current-action.put-down-command <cmd>)
   (<o> ^name attend-to-scene-change
        ^change.object-info.object-handle <handle>)
   (<cmd> ^status << sent executing >>)
-->
   (<s> ^operator <o> -)
}

sp {op_put-down1*tabletop*elaborate*evaluate-new-object*candidate-for-missing-object
   (state <s> ^name op_put-down1
              ^operator <o> +
              ^held-object.handle <obj-handle>
              ^held-object.predicates.visible false
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name attend-to-scene-change
        ^change.type new-object
        ^change <change>)
   (<obj-info> ^object-handle <obj-handle>)
-->
   (<change> ^candidate-for-missing-object <obj-info>)
}
