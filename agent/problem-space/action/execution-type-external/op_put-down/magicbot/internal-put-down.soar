sp {op_put-down1*magicbot*propose*internal-put-down
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^held-object <obj>
              ^top-state.world.robot.arm.state internal
              -^current-action.put-down-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name internal-put-down
        ^object <obj>)
}

sp {op_put-down1*magicbot*apply*internal-put-down*send-confirmation
   (state <s> ^name op_put-down1
              ^operator <o>
              ^current-action.received-yes true
              ^top-state.interaction.changes <c>
              ^agent-params.arm-simulation-level none)
   (<o> ^name internal-put-down)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type confirm-put-down
          ^complete-message true)
}

sp {op_put-down1*magicbot*apply*internal-put-down*send-message*no*dest
   (state <s> ^name op_put-down1
              ^operator <o>
              ^current-action <a>
             -^destination
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-put-down
        ^object <obj>)
   (<a> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type put-down-item-request
          ^item <obj>)
   (<a> ^sent-message true)
}

sp {op_put-down1*magicbot*apply*internal-put-down*send-message*dest*loc
   (state <s> ^name op_put-down1
              ^operator <o>
              ^current-action <a>
              ^destination.predicates.category location
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-put-down
        ^object <obj>)
   (<a> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type put-down-item-request
          ^item <obj>)
   (<a> ^sent-message true)
}

sp {op_put-down1*magicbot*apply*internal-put-down*send-message*dest*obj
   (state <s> ^name op_put-down1
              ^operator <o>
              ^current-action <a>
              ^destination <dest>
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-put-down
        ^object <obj>)
   (<dest> ^predicates.category object
           ^predicates.{ << name shape >> } <loc>)
   (<a> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type put-down-item-request
          ^item <obj>
          ^location <dest>)
   (<a> ^sent-message true)
}

sp {op_put-down1*magicbot*apply*internal-put-down*remove*grabbed
   (state <s> ^name op_put-down1
              ^operator.name internal-put-down
              ^held-object.predicates <preds>
              ^current-action <a>)
   (<a> ^received-yes true)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -
            ^arm-status not-grabbed)
   (<a> ^put-down-command.status success)
}


