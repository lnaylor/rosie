sp {op_put-down1*magicbot*magicbot*elaborate*problem-space*attend-to-all-perception*yes
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_put-down1*magicbot*elaborate*held-object*superoperator
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^action-operator.arg1.id <obj>)
-->
   (<s> ^held-object <obj>)
}

sp {op_put-down1*magicbot*elaborate*object-info
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^held-object <held>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^wm-obj <held>)
-->
   (<s> ^object-info <obj-info>)
}

sp {op_put-down1*magicbot*elaborate*destination*arg2
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^action-operator.arg2.{2} <obj>)
-->
   (<s> ^destination <obj>)
}

sp {op_put-down1*elaborate*executing-command*true
   (state <s> ^name op_put-down1
              ^current-action.{ << put-down-command face-command drive-command >> }.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_put-down1*elaborate*allow-wait*true
   (state <s> ^name op_put-down1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}


##### GOAL SATISFACTION ####

sp {op_put-down1*magicbot*elaborate*desired*satisfied*no*destination
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^current-action.put-down-command.status success
             -^destination)
-->
   (<s> ^desired.satisfied true)
}

sp {op_put-down1*magicbot*elaborate*desired*in*destination
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^current-action.put-down-command.status success
              ^destination <dest>
              ^held-object <obj>
              ^top-state.world.predicates.predicate <in-pred>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <dest>)
-->
   (<s> ^desired.satisfied true)
}

sp {op_put-down1*magicbot*elaborate*calculation
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^robot-position <rp>
              ^destination-position <op>)
   (<rp> ^x <rx> ^y <ry>)
   (<op> ^x <ox> ^y <oy>)
-->
   (<s> ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^object-x <ox> ^object-y <oy>)
}

sp {op_put-down1*magicbot*elaborate*calculations*distance
   (state <s> ^name op_put-down1
              ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^object-x <ox> ^object-y <oy>)
-->
   (<c> ^distance (sqrt (+ (* (- <ox> <rx>) (- <ox> <rx>) )
                           (* (- <oy> <ry>) (- <oy> <ry>) ) ) ) )
}

sp {op_put-down1*magicbot*elaborate*calculations*theta
   (state <s> ^name op_put-down1
              ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^object-x <ox> ^object-y <oy>)
-->
   (<c> ^theta (atan2 (- <oy> <ry>) (- <ox> <rx>) ) )
}

sp {op_put-down1*magicbot*elaborate*calcuations*drive-x*drive-y
   (state <s> ^name op_put-down1
              ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^object-x <ox> ^object-y <oy>
        ^theta <theta>)
-->
   (<c> ^drive-x (- <ox> (* 1.5 (cos <theta>) ) ) )
   (<c> ^drive-y (- <oy> (* 1.5 (sin <theta>) ) ) )
}
   
        
#
#sp {op_put-down1*magicbot*elaborate*predicate-handle*superoperator
#   (state <s> ^name op_put-down1
#              ^action-operator.arg2.handle <pred-handle>)
#-->
#	(<s> ^predicate-handle <pred-handle>)
#}
#
#sp {op_put-down1*magicbot*elaborate*destination*superoperator
#   (state <s> ^name op_put-down1
#              ^action-operator.arg2.{2} <obj>)
#-->
#   (<s> ^destination <obj>)
#}
#
#sp {op_put-down1*magicbot*elaborate*projection-result*from*current-action
#   (state <s> ^name op_put-down1
#              ^current-action.projection-result <res>)
#-->
#   (<s> ^projection-result <res>)
#}

