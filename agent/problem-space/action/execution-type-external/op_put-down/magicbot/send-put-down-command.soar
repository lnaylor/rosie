sp {op_put-down1*magicbot*propose*send-put-down-command
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
             -^destination
             -^current-action.put-down-command
             ^world.robot.arm.state external)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-put-down-command)
}

sp {op_put-down1*magicbot*propose*send-put-down-command*destination*after*face*no*drive
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^destination
              ^current-action.face-command.status success
             -^current-action.drive-command
              ^calculations.distance <= 1.5
             -^current-action.put-down-command
             ^world.robot.arm.state external)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-put-down-command)
}

sp {op_put-down1*magicbot*propose*send-put-down-command*destination*after*drive
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^destination
              ^current-action.drive-command.status success
             -^current-action.put-down-command
              ^world.robot.arm.state external)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-put-down-command)
}

# Put the command on the output link
sp {op_put-down1*magicbot*apply*send-put-down-command*send
   (state <s> ^name op_put-down1
              ^operator.name send-put-down-command
              ^io.output-link <out>
              ^current-action <a>
              ^object-info.input-link-obj.tag-id <perc-id>)
-->
   (<out> ^do-control-law <cmd>)
   (<cmd> ^name put-down
          ^termination-condition <term>)
   (<term> ^name holding
           ^parameters <params>)
   (<params> ^object-id <perc-id>
             ^negated true)
   (<a> ^put-down-command <cmd>)
}

