sp {problem-space*action*elaborate*default*problem-space
   (state <s> ^superstate.problem-space <ss-ps>
             -^default-problem-space no)
   (<ss-ps> ^name action)
-->
   (<s> ^problem-space <ps>)
   (<ps> ^name action
         ^world <world>
         ^learning <learn>
         ^perception <perc>
         ^subactions <subs>
         ^super-problem-space <ss-ps>)
}

sp {problem-space*action*copy*default*action-space*from*superstate
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^super-problem-space.action-space <as>
        -^action-space <> <as>)
-->
   (<ps> ^action-space <as>)
}

sp {problem-space*action*copy*default*execution-type*from*superstate
   (state <s> ^problem-space <ps>
             -^name selection
             -^name evaluate-operator)
   (<ps> ^name action
         ^super-problem-space.execution-type <type>
        -^execution-type <> <type>)
-->
   (<ps> ^execution-type <type>)
}

sp {problem-space*action*copy*default*action*type*from*superstate
   (state <s> ^problem-space <ps>
             -^name selection
             -^name evaluate-operator)
   (<ps> ^name action
         ^super-problem-space.action.type <type>
         ^action <a>)
   (<a> -^type <> <type>)
-->
   (<a> ^type <type>)
}
